load smt.maude
load while-semantics-basics-smtTypes.maude
set trace on .

mod WHILE-MAUDE is

    protecting WHILE-BASICS .
    protecting META-LEVEL * (op _=_ to _=M_) * (op _+_ to _+M_) .

    sort State .
    
    op < _ | _ > : Program Map{Var, Value} -> State [ctor] . 
    op errState : -> State [ctor] . 

    vars Var : Var .
    vars BExp BExp2 : BExp .
    vars IL IL1 IL2 P : Program .
    var STR : Map{Var, Value} .
    var AExp : AExp .
    var SVDeclL : Program .
    var V : Value .

    op start : Program -> State .
    eq start(P) = $start(P, nil, empty) .
    op start : Program Program -> State .
    eq start(P, nil) = $start(P, nil, empty) .
    eq start(P, (Var := V ;) SVDeclL) = $start(P, (Var := V ;) SVDeclL, empty) .
    op $start : Program Program Map{Var, Value} -> State .
    eq $start(P, nil, STR) = < P | STR > .
    eq $start(P, (Var := V ;) SVDeclL, STR) = $start(P, SVDeclL, insert(Var, V, STR)) .

    rl [skip] : 
        < skip ; IL | STR >
        =>
        < IL | STR > .

    rl [assign] : 
        < Var := AExp ; IL | STR >
        =>
        < IL | insert(Var, eval(AExp, STR), STR) > .

    crl [if..then] : 
        < if BExp then { IL1 } else { IL2 } IL | STR >
        =>
        < IL1 IL | STR >
        if metaCheck(upModule('WHILE-MAUDE, false), upTerm(eval(BExp, STR))) .

    crl [if..else] : 
        < if BExp then { IL1 } else { IL2 } IL | STR >
        =>
        < IL2 IL | STR >
        if metaCheck(upModule('WHILE-MAUDE, false), upTerm(not eval(BExp, STR))) .
    

    crl [while..loop] : 
        < while BExp do { IL1 } IL | STR >
        =>
        < IL1 while BExp do { IL1 } IL | STR >
        if metaCheck(upModule('WHILE-MAUDE, false), upTerm(eval(BExp, STR))) .
        
    crl [while..exit] : 
        < while BExp do { IL1 } IL | STR >
        =>
        < IL | STR >
        if metaCheck(upModule('WHILE-MAUDE, false), upTerm(not eval(BExp, STR))) .
endm