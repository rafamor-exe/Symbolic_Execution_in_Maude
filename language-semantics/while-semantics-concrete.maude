load ../semantics-basics/semantics-basics-maudeTypes.maude

fmod WHILE-LANG-INSTS-MOD is

    protecting CONCRETE-AEXP-MOD .

    sort Inst .

    op skip ; : -> Inst [ctor prec 20] .
    op _ := _ ; : Var IExp -> Inst [ctor prec 20] .
    op _ := _ ; : Var RExp -> Inst [ctor prec 20] .
    op _ := _ ; : Var BExp -> Inst [ctor prec 20] .

endfm

view Inst from TRIV to WHILE-LANG-INSTS-MOD is
  sort Elt to Inst .
endv

fmod WHILE-LANG-BLOCK-MOD is

    protecting WHILE-LANG-INSTS-MOD .
    protecting LIST{Inst} * (sort List{Inst} to Program) .

    op if _ then {_} else {_} : BExp Program Program -> Inst [ctor prec 20] .
    op if _ then {_} : BExp Program -> Inst [prec 20] .

    op while _ do {_} : BExp Program -> Inst [ctor prec 20] .

    var B : BExp .
    var IL1 : Program .

    eq if B then {IL1} = if B then {IL1} else {nil} .

endfm

fmod WHILE-BASICS is
    protecting WHILE-LANG-BLOCK-MOD .
endfm

mod WHILE-MAUDE is

    protecting WHILE-BASICS .

    sort State .
    
    op < _ | _ > : Program Stores -> State [ctor] . 
    op errState : Program Stores -> State [ctor] . 

    vars Var Var1 Var2 : Var .
    var IVar : IVar .
    var RVar : RVar .
    var BVar : BVar .
    vars BExp BExp2 : BExp .
    vars IL IL1 IL2 P : Program .
    vars STR STR2 : Stores .
    var ISTR : IStore .
    var RSTR : RStore .
    var BSTR : BStore .
    var IExp : IExp .
    var RExp : RExp .

    op start : Program -> State .
    eq start(P) = < P | (empty | empty | empty) > .

    rl [skip] : 
        < skip ; IL | STR >
        =>
        < IL | STR > .

    rl [assign-I] : 
        < IVar := IExp ; IL | (ISTR | RSTR | BSTR) >
        =>
        < IL | insert(IVar, val(eval(IExp, ISTR)), ISTR) | RSTR | BSTR > .
    
    rl [assign-R] : 
        < RVar := RExp ; IL | (ISTR | RSTR | BSTR) >
        =>
        < IL | ISTR | insert(RVar, val(eval(RExp, RSTR)), RSTR) | BSTR > .

    rl [assign-B] : 
        < BVar := BExp ; IL | (ISTR | RSTR | BSTR) >
        =>
        < IL | ISTR | RSTR | insert(BVar, val(eval(BExp, BSTR)), BSTR) > .

    crl [if..then] : 
        < if BExp then { IL1 } else { IL2 } IL | STR >
        =>
        < IL1 IL | STR >
        if eval(BExp, STR) .

    crl [if..else] : 
        < if BExp then { IL1 } else { IL2 } IL | STR >
        =>
        < IL2 IL | STR >
        if not eval(BExp, STR) .
    

    crl [while..loop] : 
        < while BExp do { IL1 } IL | STR >
        =>
        < IL1 while BExp do { IL1 } IL | STR >
        if eval(BExp, STR) .
        
    crl [while..exit] : 
        < while BExp do { IL1 } IL | STR >
        =>
        < IL | STR >
        if not eval(BExp, STR) .

    *** Auxiliary

    op getProgram : State -> Program .
    eq getProgram(< P | STR >) = P .

    op getStore : State -> Stores .
    eq getStore(< P | STR >) = STR .

    op updateProgram : State Program -> State .
    eq updateProgram(< P | STR >, IL) = < IL | STR > .

    op updateStore : State Stores -> State .
    eq updateStore(< P | STR >, STR2) = < P | STR2 > .
    
    *** Ignore
    op test : Bool Int -> Rat .
endm