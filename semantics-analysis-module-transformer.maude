load while-semantics-basics.maude

*** Test transformer with while language module
load while-semantics.maude

fmod MODULE-TRANSFORMER is
    pr META-LEVEL .
    pr LIST{Location} * (sort List{Location} to Path) .

    pr WHILE-SE-BASICS .

    vars T1 T2 Tcond Tmod Constraints Path : Term .
    var AttrSet : AttrSet .
    var RlSet : RuleSet .
    var Cond : Condition .
    var Header : Header .
    var ImportList : ImportList .
    var SortSet : SortSet .
    var SubsortDeclSet : SubsortDeclSet .
    var OpDeclSet : OpDeclSet .
    var MembAxSet : MembAxSet .
    var EquationSet : EquationSet .
    var InstId StateOp : Qid .
    var M : Module .
    ***var Tmod : Module .
    ***var Constraints : BExp .
    ***var Path : Path .

    *** Symbolic Execution rule structure
    op rl_{_}<{_}=>_{_}<{_}[_]. : Term Term Term Term Term Term AttrSet -> Rule [ctor] .
    op crl_{_}<{_}=>_{_}<{_}if_[_]. : Term Term Term Term Term Term Condition AttrSet -> Rule [ctor] .


    op rlToSymbolic : RuleSet -> RuleSet .
    eq rlToSymbolic(none) = none .
    eq rlToSymbolic(rl T1 => T2 [label(InstId) AttrSet] . RlSet) = rl '_`{_`}<`{_`}[T1, 'Constraints:BExp, 'Path:Path] => '_`{_`}<`{_`}[T2, 'Constraints:BExp, '__['Path:Path, 'loc[upTerm(InstId)]]] [label(InstId) AttrSet] . rlToSymbolic(RlSet) .
    *** TODO: parametrize language-specific sorts (Map, BExp)
    ceq rlToSymbolic(crl T1 => T2 if Cond [label(InstId) AttrSet] . RlSet) = crl '_`{_`}<`{_`}[T1, 'Constraints:BExp, 'Path:Path] => '_`{_`}<`{_`}[T2, 'Constraints:BExp, '__['Path:Path, 'loc[upTerm(InstId)]]] if 'Constraints':BExp := '_/\_['Constraints:BExp, downTerm(Tcond, 'false.BExp)] /\ 'metaCheck[Tmod, 'upTerm['eval['Constraints':BExp, 'STR:Map`{Var`,Value`}]]] = 'true.Bool [label(InstId) AttrSet] . rlToSymbolic(RlSet)
        if ('metaCheck[Tmod, Tcond] = 'true.Bool) := Cond .

    op inclSEImports : ImportList -> ImportList .
    eq inclSEImports(ImportList) = ImportList (protecting 'LIST{'Location} * (sort 'List`{Location`} to 'Path) .) .

    op inclSESorts : SortSet -> SortSet .
    eq inclSESorts(SortSet) = SortSet ; 'SEState ; 'ConstrainedStart ; 'Location .    

    ***op updateStateSymb : OpDeclSet Qid -> OpDeclSet .
    ***eq updateStateSymb((op 'start : 'Program -> StateOp .) OpDeclSet, StateOp)

    *** Define the Symbolic State on top of the casual (concrete) state
    op inclSEState : OpDeclSet Qid -> OpDeclSet .
    eq inclSEState(OpDeclSet, StateOp) = OpDeclSet
                                        (op '_`{_`}<`{_`} : StateOp 'BExp 'Path -> 'SEState [ctor] .)
                                        (op '_where_ : 'Program 'BExp -> 'ConstrainedStart [ctor] .)
                                        (op 'loc : 'Qid -> 'Location [ctor] .) .
                                        ***(op 'start : 'ConstrainedStart -> 'SEState [none] .)
                                        ***(op 'start : 'ConstrainedStart 'Program -> 'SEState [none] .)

    op transformModSymb : Module Qid -> Module .
    eq transformModSymb(M, StateOp) = mod 'SYMBOLIC-MOD is
                                            inclSEImports(getImports(M))
                                            sorts inclSESorts(getSorts(M)) .
                                            getSubsorts(M)
                                            inclSEState(getOps(M), StateOp) getMbs(M) getEqs(M)
                                            rlToSymbolic(getRls(M))
                                      endm .

    ***eq 'pr = 'protecting .

endfm

mod VERIFICATION-COMMANDS is

    pr MODULE-TRANSFORMER .
    pr LEXICAL .

    vars ModId StateOp SearchType : Qid .
    vars InitSEState PatternSEState : Term .
    var SearchCond : Condition .
    var Bound : Bound .
    var SolN : Nat .

    op searchSymb : Qid Qid Term Term Condition Qid Bound Nat -> ResultTriple .
    eq searchSymb(ModId, StateOp,
                  InitSEState,
                  PatternSEState,
                  SearchCond,
                  SearchType,
                  Bound,
                  SolN)
                  =
                  metaSearch(transformModSymb(upModule(ModId, false), StateOp),
                             InitSEState,
                             PatternSEState,
                             SearchCond,
                             SearchType,
                             Bound,
                             SolN) .

endm

*** Test transformer with while language module
***mod TEST-TRANSFORM is
***    pr MODULE-TRANSFORMER .
***    pr WHILE-MAUDE .
***
***    ***op searchSymb : Qid SEState 
***
***endm