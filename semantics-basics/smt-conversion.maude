load smt.maude
fmod SMT-CONVERSION is
    pr REAL-INTEGER .
    pr META-LEVEL .
    pr LEXICAL .
    pr CONVERSION .

    op toInteger : Int -> Integer .
    op toReal : Int -> Real .
    op toReal : Rat -> Real .
    op toReal : Float -> Real .
    op toBoolean : Bool -> Boolean .

    op toInt : Integer -> Int .
    op toRat : Real -> Rat .
    op toBool : Boolean -> Bool .

    op errInteger : -> Integer [ctor] .
    op errReal : -> Real [ctor] .
    op errBoolean : -> Boolean [ctor] .
    op errInt : -> Int [ctor] .
    op errRat : -> Rat [ctor] .
    op errBool : -> Bool [ctor] .

    var T : Term .
    var TL : TypeList .
    var MConst : Constant .

    op toMaudeSorts : TypeList -> TypeList .
    eq toMaudeSorts(nil) = nil .
    eq toMaudeSorts('Boolean TL) = 'Bool toMaudeSorts(TL) .
    eq toMaudeSorts('Integer TL) = 'Int toMaudeSorts(TL) .
    eq toMaudeSorts('Real TL) = 'Rat toMaudeSorts(TL) .
    eq toMaudeSorts(T TL) = T toMaudeSorts(TL) [owise] .

    op subMConstSorts : Constant -> Constant .
    eq subMConstSorts(MConst) = qid(string(getName(MConst)) + "." + string(toMaudeSorts(getType(MConst)))) .


    var I : Int .
    var Ismt : Integer .
    var R : Rat .
    var Rsmt : Real .
    var B : Bool .
    var Bsmt : Boolean .
    var F : Float .
    
    eq toInteger(I) = downTerm(getTerm(metaParse(['INTEGER], tokenize(string(I, 10)), 'Integer)), errInteger) .
    eq toReal(I) = toReal(toInteger(I)) .
    eq toReal(R) = downTerm(getTerm(metaParse(['REAL], tokenize(string(R, 10)), 'Real)), errReal) .
    ceq toReal(F) = toReal(I)
        if I := rat(F) .
    eq toReal(F) = downTerm(getTerm(metaParse(['REAL], tokenize(string(rat(F), 10)), 'Real)), errReal) .
    eq toBoolean(B) = downTerm(getTerm(metaParse(['BOOLEAN], getName(upTerm(B)), 'Boolean)), errBoolean) .

    eq toInt(Ismt) = downTerm(getTerm(metaParse(['INT], getName(upTerm(Ismt)), 'Int)), errInt) .
    eq toRat(Rsmt) = downTerm(getTerm(metaParse(['RAT], getName(upTerm(Rsmt)), 'Rat)), errRat) .
    eq toBool(Bsmt) = downTerm(getTerm(metaParse(['BOOL], getName(upTerm(Bsmt)), 'Bool)), errBool) .

endfm